buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {


    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "checkstyle"

    def checkstyleVersion = "10.4"

    version = '1.0'
    ext {
        appName = "Piazza Panic"
        gdxVersion = '1.11.0'
        roboVMVersion = '2.3.16'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }

    configurations {
	checkstyleRules
    }

    dependencies {
	checkstyleRules(
		"com.puppycrawl.tools:checkstyle:${checkstyleVersion}") {
			transitive = false
		}
    }

    checkstyle {
	toolVersion "${checkstyleVersion}"
    	config = resources.text.fromArchiveEntry(
		configurations.checkstyleRules, 'google_checks.xml')
    }
}

project(":desktop") {
    apply plugin: "java-library"


    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

    }
    
    jar {
        manifest {
	     attributes('Main-Class': 'desktop.src.cs.eng1.piazzapanic.DesktopLauncher')
        }
     }
}

project(":core") {
    apply plugin: "java-library"


    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

    }
}

